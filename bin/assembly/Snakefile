rule metaSPAdes:
    """
    metaSPAdes assembly 
    """
    input:
        read1 = mgsim_dir + '{rep}/1/R1.fq',
	read2 = mgsim_dir + '{rep}/1/R2.fq'
    output:
        contigs = temp(asmbl_dir + '{rep}/metaspades/contigs.fasta')
    params:
        params = config['params']['assemblers']['metaspades'],
	tmp_dir = os.path.join(config['tmp_dir'], '{rep}')
    conda:
        '../envs/assembly.yaml'
    threads:
        12     
    resources:
        time = lambda wildcards, attempt: attempt ** 3 * 60 * 4,
        n = lambda wildcards, attempt: 12,
        mem_gb = lambda wildcards, attempt: attempt * 32,
        mem_gb_pt = lambda wildcards, attempt: int(round(attempt * 32 / 8 * 1.1, 0))
    log:
        log_dir + 'metaspades/{rep}.log'
    benchmark:
        benchmark_dir + 'metaspades/{rep}.txt'
    shell:
        """
        OUTDIR=`dirname {output.contigs}`
        if [ -d "$OUTDIR" ]; then
          echo "Removing directory: $OUTDIR" > {log}
          rm -rf $OUTDIR 2>> {log}
        fi

        spades.py --meta {params.params} \
          -t {threads} -m {resources.mem_gb} \
	  --tmp-dir {params.tmp_dir} -o $OUTDIR \
          -1 {input.read1} -2 {input.read2} \
          >> {log} 2>&1
        """
	
rule megahit:
    """
    metahit assembly 
    """
    input:
        read1 = mgsim_dir + '{rep}/1/R1.fq',
	read2 = mgsim_dir + '{rep}/1/R2.fq'
    output:
        contigs = temp(asmbl_dir + '{rep}/megahit/contigs.fasta')
    params:
        params = config['params']['assemblers']['megahit'],
	tmp_dir = os.path.join(config['tmp_dir'], '{rep}')
    conda:
        '../envs/assembly.yaml'
    threads:
        12     
    resources:
        time = lambda wildcards, attempt: attempt ** 3 * 60 * 4,
        n = lambda wildcards, attempt: 12,
        mem_bytes = lambda wildcards, attempt: int(attempt * 3.2e10),
        mem_gb_pt = lambda wildcards, attempt: int(round(attempt * 32 / 8 * 1.1, 0))
    log:
        log_dir + 'metaspades/{rep}.log'
    benchmark:
        benchmark_dir + 'metaspades/{rep}.txt'
    shell:
        """
        OUTDIR=`dirname {output.contigs}`
        if [ -d "$OUTDIR" ]; then
          echo "Removing directory: $OUTDIR" > {log}
          rm -rf $OUTDIR 2>> {log}
        fi

	megahit {params.params} \
          -t {threads} \
          -m {resources.mem_bytes} \
	  -o $OUTDIR \
          -1 {input.read1} \
          -2 {input.read2} \
          >> {log} 2>&1

        mv -f $OUTDIR"/final.contigs.fa" {output.contigs} 2>> {log}
        """

rule contig_length_filter:
    """
    Filtering out short length contigs
    """
    input:
        asmbl_dir + '{rep}/{assembler}/contigs.fasta'
    output:
        asmbl_dir + '{rep}/{assembler}/contigs_filtered.fasta'
    params:
        exe = config['pipeline']['script_folder'] + 'contig_len_cutoff.py',
        cutoff = config['params']['contig_length_cutoff']
    conda:
        '../envs/MGSIM.yaml'
    resources:
        time = lambda wildcards, attempt: attempt ** 2 * 59,
        mem_gb_pt = lambda wildcards, attempt: attempt ** 2 + 2
    log:
        log_dir + 'contig_length_filter/{rep}/{assembler}.log',    
    benchmark:
        benchmark_dir + 'contig_length_filter/{rep}/{assembler}.txt'
    shell:
        """
        {params.exe} --length {params.cutoff} {input} > {output} 2> {log}
        """


