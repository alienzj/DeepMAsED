import os
import pandas as pd

# rule combine_ref_genomes:
#     """
#     Combining reference genomes
#     """
#     input:
#         config['genomes_tbl']['Fasta']
#     output:
#         temp(config['tmp_dir'] + 'ref_genomes.fna')
#     resources:
#         time = lambda wildcards, attempt: attempt ** 3 * 59
#     log:
#         log_dir + 'combine_ref_genomes.log'
#     benchmark:
#         benchmark_dir + 'combine_ref_genomes.txt'
#     shell:
#         """
#         cat {input} > {output} 2> {log}
#         """

def get_ref_genomes(wildcards):
    # simulated metagenome community
    wAbund = os.path.join(mgsim_dir, wildcards[0], 'comm_wAbund.txt')
    wAbund = pd.read_csv(wAbund, sep='\t')
    # genome fasta files
    fna = config['genomes_tbl']
    # inner join & return
    return pd.merge(wAbund, fna, on='Taxon', how='inner')['Fasta'].tolist()

rule combine_ref_genomes:
    """
    Combining reference genomes.
    Only using those included in the metagenome community
    """
    input:
        fna = config['genomes_file'],
        comm = mgsim_dir + '{rep}/comm_wAbund.txt' 
    output:
        temp(config['tmp_dir'] + '{rep}/ref_genomes.fna')
    params:
        exe = config['pipeline']['script_folder'] + 'concat_ref_genomes.py',
	genomes_dir = genomes_dir
    resources:
        time = lambda wildcards, attempt: attempt ** 3 * 59
    log:
        log_dir + '{rep}/combine_ref_genomes.log'
    benchmark:
        benchmark_dir + '{rep}/combine_ref_genomes.txt'
    shell:
        """
        {params.exe} {input.fna} {input.comm} {params.genomes_dir} \
           > {output} 2> {log}
        """

rule minimap2_contigs_vs_refs:
    """
    minimap2 mapping of contigs to ref genomes
    """
    input:
        contigs = asmbl_dir + '{rep}/{assembler}/contigs.fasta',
        refs = config['tmp_dir'] + '{rep}/ref_genomes.fna'
    output:
        aln = temp(config['tmp_dir'] + '{rep}/{assembler}/minimap2_aln.paf')
    params:
        config['params']['minimap2']
    threads:
        12
    resources:
        time = lambda wildcards, attempt: attempt ** 3 * 59,
        n = lambda wildcards, attempt: 12,
        mem_gb_pt = lambda wildcards, attempt: attempt ** 2 * 2
    conda:
        '../envs/quast.yaml'
    log:
        log_dir + 'minimap2/{rep}/{assembler}.log'
    benchmark:
        benchmark_dir + 'minimap2/{rep}/{assembler}.txt'
    shell:
        """
        echo "" > {log}
        minimap2 -t {threads} --secondary=no -c \
          {input.refs} {input.contigs} 2>> {log} | \
          sort -k 1 > {output.aln} 2>> {log}
        """

rule paf_compress:
    """
    compressing the PAF file
    """
    input:
        config['tmp_dir'] + '{rep}/{assembler}/minimap2_aln.paf'
    output:
        true_errors_dir + '{rep}/{assembler}/minimap2_aln.paf.gz'        
    resources:
        time = lambda wildcards, attempt: attempt ** 3 * 59
    log:
        log_dir + '{rep}/{assembler}/paf_compress.log'
    benchmark:
        benchmark_dir + '{rep}/{assembler}/paf_compress.txt'
    shell:
        """
        gzip -c {input} > {output} 2> {log}
        """

rule minimap2_contigs_vs_refs_summary:
    """
    Summarizing the PAF file generated by minimap2
    """
    input:
        aln = config['tmp_dir'] + '{rep}/{assembler}/minimap2_aln.paf'
    output:
        true_errors_dir + '{rep}/{assembler}/minimap2_aln_summary.tsv'
    params:
        exe = config['pipeline']['script_folder'] + 'paf_summarize.py'
    resources:
        time = lambda wildcards, attempt: attempt ** 3 * 59
    log:
        log_dir + '{rep}/{assembler}/minimap2_contigs_vs_refs_summary.log'
    benchmark:
        benchmark_dir + '{rep}/{assembler}/minimap2_contigs_vs_refs_summary.txt'
    shell:
        """
        {params.exe} {input.aln} > {output} 2> {log}
        """

rule metaquast:
    """
    MetaQUAST assessment of assemblies
    """
    input:
        contigs = asmbl_dir + '{rep}/{assembler}/contigs.fasta',
        refs = config['genomes_tbl']['Fasta'],
        read1 = mgsim_dir + '{rep}/1/R1.fq',
	read2 = mgsim_dir + '{rep}/1/R2.fq'
    output:
        done = touch(true_errors_dir + '{rep}/{assembler}/metaquast.done')
    params:
        config['params']['metaquast']
    threads:
        12
    resources:
        time = lambda wildcards, attempt: attempt ** 3 * 59,
        n = lambda wildcards, attempt: 12,
        mem_gb_pt = lambda wildcards, attempt: attempt * 2
    conda:
        '../envs/quast.yaml'	
    log:
        log_dir + 'metaquast/{rep}/{assembler}.log'
    benchmark:
        benchmark_dir + 'metaquast/{rep}/{assembler}.txt'
    shell:
        """
        OUTDIR=`dirname {output.done}`
        REFS=`echo {input.refs} | perl -pe 's/ /,/g'`

        metaquast.py {params} -t {threads} \
          --reads1 {input.read1} --reads2 {input.read2} \
          -o $OUTDIR -R $REFS \
          {input.contigs} 2>&1 > {log}
        """
 
